plugins {
	id 'java-library'
	id 'eclipse'
}

defaultTasks 'clean', 'build', 'publishToMavenLocal'

ext.getGitHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception ignored) {
        return "unknown"
    }
}

// We embed the git hash into jar files, and also use it for the plugin version of snapshot builds.
ext.buildVersion = version + '+' + getGitHash()
ext.isSnapshot = version.contains('-SNAPSHOT')
ext.pluginVersion = isSnapshot ? buildVersion : version

println 'Project version: ' + version
println 'Build version: ' + buildVersion
println 'Plugin version: ' + pluginVersion

repositories {
    mavenLocal()
    mavenCentral()

    // Bukkit/Spigot API
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }

    // Shopkeepers API
    maven { url 'https://raw.githubusercontent.com/Shopkeepers/Repository/main/releases/' }

    // Phoenix Dev (MMOItems & MythicLib)
    maven { url 'https://nexus.phoenixdevt.fr/repository/maven-public/' }

    maven { url 'https://mvn.lumine.io/repository/maven-public/' }
}

dependencies {
    // Spigot API
    compileOnly 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'

    // Shopkeepers API
    compileOnly 'com.nisovin.shopkeepers:ShopkeepersAPI:2.23.3'

    // MMOItems & MythicLib
    compileOnly 'io.lumine:MythicLib-dist:1.6.2-SNAPSHOT'
    compileOnly 'net.Indyuce:MMOItems-API:6.9.5-SNAPSHOT'

    // Testing
    testImplementation 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'
    testImplementation 'junit:junit:4.13.1'
}

java
{
	sourceCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile) {
	options.encoding = 'utf8'
}

processResources {
	inputs.property 'pluginVersion', pluginVersion
	inputs.property 'dboUrl', dboUrl
	inputs.property 'description', description

	from project.sourceSets.main.resources.srcDirs

	filesMatching('plugin.yml') {
		expand([
			'pluginVersion': pluginVersion,
			'dboUrl': dboUrl,
			'description': description
		])
	}

	// TODO Some plugins might add resource directories twice.
	// See https://github.com/gradle/gradle/issues/17236
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar {
    manifest {
        attributes 'Implementation-Title': "${project.group}:${project.name}",
                'Implementation-Version': project.buildVersion

        def deps = configurations.compileClasspath.resolvedConfiguration.firstLevelModuleDependencies
                .collectEntries{[it.moduleName, it.moduleVersion]}
        attributes deps
    }
}